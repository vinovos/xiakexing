#nop 桃花新手走桃林迷宫任务；在陆乘风处触发

#class taohua_taolin kill;
#class taohua_taolin open;
#var {fangwei_table}{
    {东} {e}
    {南} {s}
    {西} {w}
    {北} {n}
    {东南} {se}
    {东北} {ne}
    {西南} {sw}
    {西北} {nw}
};
#var {fangwei_walk} {
    {乾天}{w;nw;w;s}
    {兑泽}{w;sw;nw;s}
    {离火}{w;w;sw;nw}
    {震雷}{w;sw;w;n}
    {巽风}{sw;w;w;sw}
    {坎水}{nw;w;sw;nw}
    {艮山}{sw;w;w;s}
    {坤地}{nw;nw;w;e}
};

sc;
ask lu about job;
#act {^陆乘风说道：「庄子附近最近来一位女飞贼，你还是先去练练功夫吧，以免碰到被人耻笑} {
    #line {log} {logs/event.log} {"开始出飞贼啦，准备学技能"};
    #read utils/taohua_newbie/taohua_skills.tt;
}
#act {陆乘风说道：「${user.name}，我为你占得一卦：」} {
    #delay {taolin_delay} {ask lu about job} {60};
}
#act {陆乘风说道：「我不是刚给你占过一卦吗？」} {
    #delay {taolin_delay} {ask lu about job} {60};
}
#act {你向陆乘风打听有关『job』的消息。} {
    #act {陆乘风说道：「没看我正忙着嘛？」} {
        #delay {taolin_delay} {ask lu about job} {5};
    }
}

#var taolin_walk_flag 0;
#list taolin_walk_list {create};

#act {陆乘风说道：「${user.name}，我为你占得一卦：%*。」} {
    #var fangwei {%1};
    #list taolin_walk_list {create} $fangwei_walk[%1];
    do 4 e;
    #echo 开始走迷宫;
    walk_taolin;
}

#act {天色大变，陆乘风给你算的卦也不那么准了} {
    #var taolin_walk_flag 0;
    #undelay taolin_delay1;
    #undelay taolin_delay2;
    #undelay taolin_delay3;
    #undelay taolin_delay4;

    #class tmp open;
    #act {这里是归云庄的大厅，是庄主会客的地方，里面站着两排卫士}{
        #var current_position {归云庄};
    };
    #act {这里是一大片的桃花阵，隐含八卦九宫位置。如果没有办法破解，你也可以选择离开} {
        #var current_position {桃花阵};
    };
    #class tmp close;
    l;
    #delay 3 {
        #if {"$current_position" == "桃花阵"} {leave;do 3 w;ask lu about job;};
        #if {"$current_position" == "归云庄"} {ask lu about job;};
    }
}

#act {一个女飞贼冲了出来，似乎来意不善。}{
    #class taolin_feizei_temp open;
    #var taolin_walk_flag 0;
    #act {不解决了%*，你恐怕很难离开}{
        #undelay taolin_delay1;
        #undelay taolin_delay2;
        #undelay taolin_delay3;
        #undelay taolin_delay4;

        #echo {"飞贼名字为%%1"};
        #var taolin_feizei %%1;
        id here;
        #act {$taolin_feizei%s={.*},%*} {
            #echo {"飞贼id为%%%3"};
            #var taolin_feizei_id %%%3;
            yun qi;
            kill %%%3;
            #delay 1 {kill %%%3;};
        };

        #act {你战胜了$taolin_feizei} {
            #delay 1 kill $taolin_feizei_id;
            #var taolin_walk_flag 1;
            #delay 1 {#class taolin_feizei_temp kill};
            #delay 2 walk_taolin;
        }
    };
    #class taolin_feizei_temp close;
};

#alias {walk_taolin} {
    #if {$taolin_walk_flag} {
        #delay {taolin_delay1} {$taolin_walk_list[+1]} {2.5};
        #delay {taolin_delay2} {$taolin_walk_list[+2]} 5;
        #delay {taolin_delay3} {$taolin_walk_list[+3]} {7.5};
        #delay {taolin_delay4} {$taolin_walk_list[+4]; walk_taolin;} {10};
    };
}

#act {^陆乘风道：“师父喜爱古玩字画，我搜集一些古玩字画在书房} {
    s;s;na;
    #act {^你拣起了一套古玩} { jianding guwan; };
    #act {^你拣起了一套字画} { jianding zihua; };
    #act {^你发现手中的竟是%*}{
        n;n;w;w;give lu all;
    };
    #act {^陆冠英接过你手里的%*，道：不错，这是太师父喜欢的东西} {#delay 1 {e;e;ask lu about job} };
}

#act {^陆乘风说道：「你刚做完鉴定任务，先去休息一会吧。」} {#delay 10 {ask lu about job}};

#class taohua_taolin close
