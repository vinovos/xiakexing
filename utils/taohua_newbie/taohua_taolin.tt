#nop 桃花新手走桃林迷宫任务；在陆乘风处触发

#class taohua_taolin kill;
#class taohua_taolin open;
#var {fangwei_table}{
    {东} {e}
    {南} {s}
    {西} {w}
    {北} {n}
    {东南} {se}
    {东北} {ne}
    {西南} {sw}
    {西北} {nw}
};
sc;
ask lu about job;
#act {^陆乘风说道：「庄子附近最近来一位女飞贼，你还是先去练练功夫吧，以免碰到被人耻笑} {
    #line {log} {logs/event.log} {"开始出飞贼啦，准备学技能"};
    #read utils/taohua_newbie/taohua_skills.tt;
}
#act {陆乘风说道：「${user.name}，我为你占得一卦：」} {
    #delay {taolin_delay} {ask lu about job} {60};
}
#act {陆乘风说道：「我不是刚给你占过一卦吗？」} {
    #delay {taolin_delay} {ask lu about job} {60};
}
#act {你向陆乘风打听有关『job』的消息。} {
    #act {陆乘风说道：「没看我正忙着嘛？」} {
        #delay {taolin_delay} {ask lu about job} {60};
    }
}

#var taolin_walk_flag 0;
#list taolin_walk_list {create};

#act {陆乘风说道：「${user.name}，我为你占得一卦：%*。」} {
    #var fangwei {%1};
    #class tmp open;
    #act {陆乘风用手指了指墙上的八卦图} {
        #var taolin_walk_flag 1;
        look tu;
        #list taolin_walk_list {create};
        #act {$fangwei：%* %* %* %*} {
            #list taolin_walk_list add $fangwei_table[%%%1];
            #list taolin_walk_list add $fangwei_table[%%%2];
            #list taolin_walk_list add $fangwei_table[%%%3];
            #list taolin_walk_list add $fangwei_table[%%%4];
        };
        #delay 3 {
            do 4 e;
            #echo "------------------";
            walk_taolin;
        };
        #class tmp kill;
    };
    #class tmp close;
}

#act {天色大变，陆乘风给你算的卦也不那么准了} {
    #var taolin_walk_flag 0;
    #class tmp open;
    #act {这里是归云庄的大厅，是庄主会客的地方，里面站着两排卫士}{
        #var current_position {归云庄};
    };
    #act {这里是一大片的桃花阵，隐含八卦九宫位置。如果没有办法破解，你也可以选择离开} {
        #var current_position {桃花阵};
    };
    #class tmp close;
    l;
    #delay 10 {
        #if {"$current_position" == "桃花阵"} {leave;do 3 w;n;e;sleep;};
        #if {"$current_position" == "归云庄"} {ask lu about job;};
    }
}

#act {你一觉醒来，精神抖擞地活动了几下手脚}{
    w;s;gf bosss;ask lu about job;
}

#class taolin_feizei_temp open;
#act {一个女飞贼冲了出来，似乎来意不善。}{
    #var taolin_walk_flag 0;
    #act {不解决了%*，你恐怕很难离开}{
        #var taolin_feizei %%1;
        id here;
        #act {$taolin_feizei%s=%*,%*} {
            kill %%%3;
        }
    }
}
#class taolin_feizei_temp close;

#act {你已经通过桃花岛入门任务获得了}{
    #var taolin_walk_flag 1;
    #class taolin_feizei_temp kill;
    walk_taolin;
}

#alias {walk_taolin} {
    #if {$taolin_walk_flag} {
        #delay {2.5} $taolin_walk_list[+1];
        #delay 5 $taolin_walk_list[+2];
        #delay {7.5} $taolin_walk_list[+3];
        #delay {10} {$taolin_walk_list[+4]; walk_taolin;};
    };
}
#class taohua_taolin close
